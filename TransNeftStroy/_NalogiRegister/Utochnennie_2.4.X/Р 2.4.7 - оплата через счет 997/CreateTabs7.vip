#include PickBuh.vih
Interface getNastrOSNrec AlwaysReturn;
create view
var
  NastrOSNrec
        :comp;

from NastrOS
where ((
                   'Бухгалтерский учет'==NASTROS.Name
             and 16==NastrOS.TiDK
           ));

parameters
  NastrOSNrec;

  HandleEvent
    cmInit:{
      if (GetFirst  NastrOS = tsok){
        NastrOSNrec:=NastrOS.Nrec;
      } else {
        NastrOSNrec:=0;
      }
      abort;
    }
  end;
end.

Interface CheckSaldo AlwaysReturn;
var
  dSal
     :date;

  Saldo
     :double;

create view
from SALDMOUN,SpKau, PickBuh
where ((
                    dSal==SaldMOun.DateSal
             and SaldMOun.KAUOS[3]/==SpKau.nrec
             and cgKau_FpCO == PickBuh.KodTable
           ))and '968'=SALDMOUN.DBSCHETO
             and ('2'=SALDMOUN.SUBOSSCH
                       and (
                         'Расходы на оплату труда (кроме вознаграждений по итогам работы за год)'=SpKau.Name or
                         'Расходы по вознаграждениям по итогам работы за год, относящиеся к текущему периоду'=SpKau.Name
                       )
                    or
                     '3'=SALDMOUN.SUBOSSCH
                       and (
                         'расходы на оплату труда'=SpKau.Name
                       )
                    or
                     '4'=SALDMOUN.SUBOSSCH
                       and (
                         'Расходы на оплату труда (кроме вознаграждений по итогам работы за год)'=SpKau.Name or
                         'Расходы по вознаграждениям по итогам работы за год, относящаяся к текущему периоду'=SpKau.Name
                       )
                    );

parameters
  dSal,Saldo;

  HandleEvent
    cmInit:{
      Saldo:=0;
      _loop SaldMOun{
        if   SALDMOUN.VIOB = word(1)
          Saldo:=Saldo + saldmoun.sums;
        else
          Saldo:=Saldo - saldmoun.sums;
      }
      abort;
    }
  end;
end.


Interface CreateTabs AlwaysReturn;
var
  dFrom
, dTo
     :date;

   NastrOSNrec
  , nRecCO     :comp; //Фильтр по филиалам
  FullObSum : double;
#include 2.4.X_Oborot997.var
create view
from KatOs
  ,FPCO        // фильтр филиалы
  ,Vidnorma
  ,KAUREFF
  ,dogovor
  ,synonym KAUREFF VipPr
  ,synonym SpKau SpKauVidPr
  , NastrOs
  , SpKatOs
  ,synonym KAUREFF Schet
  ,synonym KAUREFF VidStrah
  ,synonym SpKau SpKauShet
  ,synonym SpKau SpKauVidStrah
  ,SALDMOUN
  ,SALDMOUN SALDMOUNK
  ,oborot
  ,katPodr

  ,StoimStruct
  ,AttrNam AttrNamSch
  ,AttrVal AttrValSch
  ,SpKau SpKauSch

  ,AttrNam AttrNamVid
  ,AttrVal AttrValVid
  ,SpKau SpKauVid
  ,ArcIznos
  ,TabKatOS
#include 2.4.X_Oborot997_From.vpp
where ((
        16==KatOs.TIDK  // фильтр на НМА
   and cgKau_FpCO == PickBuh.KodTable
   and  PickBuh.PickRec ==  KatOS.cCO  (NoIndex)       // фильтр филиалы
   and  PickBuh.PickRec ==  FPCO.nRec           // фильтр филиалы

   and KatOs.CVID/==VIDNORMA.nrec and (SubStr(VIDNORMA.Kod,1,4)='1103') //Личное страхование
   and '3000'==KAUREFF.COTABLE //Внешнее КАУ договора к каталогу ОС
   and KatOs.Nrec==KAUREFF.cRec
   and '14'==KAUREFF.WKAU
   and KAUREFF.CKAU==DOGOVOR.Nrec
   and KatOs.CPODR==KatPodr.Nrec //Подразделение учета
   and '3000'==VipPr.COTABLE //Внешнее КАУ виды продукции, работ, услуг
   and KatOs.Nrec==VipPr.cRec
   and '20009'==VipPr.WKAU
   and VipPr.CKAU==SpKauVidPr.Nrec
   and '3000'==VidStrah.COTABLE //Внешнее КАУ виды страхования
   and KatOs.Nrec/==VidStrah.cRec
   and '20025'==VidStrah.WKAU
   and VidStrah.CKAU/==SpKauVidStrah.Nrec and (SpKauVidStrah.CODE='002'       // Фильтр по виду страхования
                                          or SpKauVidStrah.CODE='014'
                                          or SpKauVidStrah.CODE='014001'
                                          or SpKauVidStrah.CODE='014002')
   and NastrOSNrec== SpKatOs.CNASTROS
   and KatOs.Nrec== SpKatOs.CKATOS
   and '3000'==Schet.COTABLE //Внешнее КАУ счет БУ для начисления амортизации
   and KatOs.Nrec==Schet.cRec
   and '20077'==Schet.WKAU
   and Schet.CKAU==SpKauShet.Nrec
   //-----------
   and 'N997'     == SALDMOUN.SCHETO    //Условие на сальдо для суммы оплаты на начало периода
   and dFrom      == SALDMOUN.DATESAL
   and '33'       == SALDMOUN.SUBOSSCH
   and KatOs.Nrec == SALDMOUN.KAUOS[2] (noindex)
 //  and word(1)    == SALDMOUN.VIOB    (noindex)
   //-----------
   and 'N997'     == SALDMOUNK.SCHETO    //Условие на сальдо для суммы оплаты на начало периода
   and dFrom      == SALDMOUNK.DATESAL
   and '33'       == SALDMOUNK.SUBOSSCH
   and KatOs.Nrec == SALDMOUNK.KAUOS[2] (noindex)
   and word(2)    == SALDMOUNK.VIOB    (noindex)
   //-----------
   and 'N997'==oborot.SCHETO  //Условие на оборот для суммы оплаты за период
   and '33'==oborot.SUBOSSCH
   and  dFrom<<=oborot.DATOB
   and  dTo>>=oborot.DATOB
   and KatOs.Nrec==oborot.KAUOS[2] (noindex)
 //------------------------------------------
//------------------------------------------
   and KatOS.nrec==StoimStruct.cDoc
   and word(16)==StoimStruct.wType

   and 'СчетЗатрат'==AttrNamSch.name
   and coStoimStruct==AttrNamSch.wtable
   and AttrNamSch.nrec==AttrValSch.cattrnam
   and StoimStruct.nrec==AttrValSch.cRec(noindex)
   and coStoimStruct==attrvalSch.wtable(noindex)
   and AttrValSch.vComp==SpKauSch.nrec

   and 'Виды продукции, работ, услуг'==AttrNamVid.name
   and coStoimStruct==AttrNamVid.wtable
   and AttrNamVid.nrec==AttrValVid.cattrnam
   and StoimStruct.nrec==AttrValVid.cRec(noindex)
   and coStoimStruct==attrvalVid.wtable(noindex)
   and AttrValVid.vComp==SpKauVid.nrec
   and KatOS.Nrec==ArcIznos.cKatOS
   and NastrOSNrec==ArcIznos.cNastrOS
#include 2.4.X_Oborot997_Where.vpp
  ))
   and dFrom<=ArcIznos.DATA
   and dTo>=ArcIznos.DATA;


parameters
  dFrom,dTo,VidDecl,DataProv, VsegoSumm999, VsegoUtoch997_34;      // фильтр филиалы  + NameCO

#include 2.4.X_Oborot997.vpp

  HandleEvent
    cmInit:{
  delete all from TabKatOS;
  delete all from TabStoimStruct;

  RunInterface(GetNastrOSNrec,NastrOSNrec);
 _Loop PickBuh
 _loop KatOS
 {
   SetVisualHeader('Обработка карточки № '+ KatOS.InNum);
   GetSaldo997_33;
   GetSaldo997_34;
   GetOborot997_33;
   GetOborot97_01;

   if Saldo997_33 <> 0 or Oborot997_33 <> 0
   {
     if (Saldo997_33 = Saldo997_34) and Oborot997_33 = 0
        Continue;
//-----------------------------
     GetUtochSum;
//-----------------------------
     insert TabKatOS;
     TabKatOS._NrecKatOS:=KatOS.nrec;
     TabKatOS._NameKatOS:=KatOS.NameOS;
     if (isValid(#Dogovor))
     {
       TabKatOS._NoDog:=Dogovor.NoDoc;
       TabKatOS._DDog:=DOGOVOR.DDoc;
     }
     else
     {
       TabKatOS._NoDog:='';
       TabKatOS._DDog:=date(0,0,0);
     }
      TabKatOS._Kol:=KatOS.PSNum;

     if (Saldo997_34 + Oborot97_01 >= 0)
     {
       if ((Saldo997_33 + Oborot997_33)<=(Saldo997_34 + Oborot97_01))
         TabKatOS._Sum:=Saldo997_33 + Oborot997_33
       else
         TabKatOS._Sum:=Saldo997_34 + Oborot97_01;
     }
     else
       if ((Saldo997_33 + Oborot997_33)>=(Saldo997_34 + Oborot97_01))
         TabKatOS._Sum:=Saldo997_33 + Oborot997_33
       else
         TabKatOS._Sum:=Saldo997_34 + Oborot97_01;

     if (Saldo997_34 + Oborot97_01 >= 0)
     {
       if Saldo997_33 <= Saldo997_34
         TabKatOS._Sum:=TabKatOS._Sum - Saldo997_33
       else
         TabKatOS._Sum:=TabKatOS._Sum - Saldo997_34;
     }
     else
       if Saldo997_33 >= Saldo997_34
         TabKatOS._Sum:=TabKatOS._Sum - Saldo997_33
       else
         TabKatOS._Sum:=TabKatOS._Sum - Saldo997_34;

     TabKatOS._Schet:=GetNalogShet(SubStr(SpKauShet.Name,1,2), SubStr(SpKauShet.Name,3,2));
     //*****************************
     if (isValid(#SpKauVidPr))
       TabKatOS._Vid:=SpKauVidPr.NAME+'/'+KatPodr.Name;
     else
       TabKatOS._Vid:=KatPodr.Name;
     //______________________
     if (KATOS.DATEK>=dFrom)
       TabKatOS._d1:=KATOS.DATEK;
     else
       TabKatOS._d1:=dFrom;
     if (KATOS.DATV<=dTo and KATOS.DATV!=date(0,0,0))
       TabKatOS._d2:=KATOS.DATV;
     else
       TabKatOS._d2:=dTo;

    TabKatOS._UtochSum := SumUtoch997_34;
    TabKatOS._Sum999 := SumProv999;

      update current TabKatOS;
  //        _loop StoimStruct{
  //          insert TabStoimStruct;
  //            _cTabKatOS:=KatOS.nrec;
  //            if (isValid(#SpKauSch)) _Sch:=SpKauSch.Name;
  //            if (isValid(#SpKauVid)) _Name:=SpKauVid.Name;
  //            _Percent:=StoimStruct.SumRes1;
  //          update current TabStoimStruct;
  //        }
      FillTabStoim;
    }
  }
  mtChangeRefCount(#TabKatOS,1);
  mtChangeRefCount(#TabStoimStruct,1);
  abort;
}

end;
end.
