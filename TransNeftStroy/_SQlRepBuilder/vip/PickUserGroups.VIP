//********************************************************************************
//
// Галактика 8.1 - Отчеты пользователя
// Отчеты на DSQL запросах
// Автор Стрельцов С.А. (отдел ПНР) 2012 год
//********************************************************************************
//──────────────────────────────────────────────────────────────────────
Interface PickUserGroups 'Выбор групп пользователей'
                  DoAccept, EscClose, Cyan;
//──────────────────────────────────────────────────────────────────────
  show at ( , 6, 80 , 25);
Create View
var
  lMarker: longint;
  MarkerName : string;
From
 Groups

Condition bySelected = foundmarker(lMarker, Groups.atl_nrec)
 ;
Parameters
  MarkerName;

Browse  br1 (,,sci1InsPM);
Table Groups;
//show at ( , , , 7);
Fields
  { Font = {Color =
         if(FoundMarker(lMarker, Groups.Atl_Nrec), 19, 0)}};
  if(foundmarker(lMarker, Groups.Atl_Nrec),'√', ''):[1], skip;
  Groups.Name 'Группа пользователей' : [50], Protect;

end;
HandleEvent
cmDefault:
{
  if GetMarkerCount(lMarker) = 0
    InsertMarker(lMarker, Groups.Atl_Nrec);
}

cmMarkUnMark:
{
  if (FoundMarker(lMarker, Groups.Atl_Nrec))
  {
    DeleteMarker(lMarker, Groups.Atl_Nrec);
  }
  else
  {
    InsertMarker(lMarker, Groups.Atl_Nrec);
  }
  if (getnext Groups)=tsOk {};

  SetWindowTitle(wnMainWindow, 'Выбор групп пользователей. Выбрано:'+ GetMarkerCount(lMarker));
  ReReadRecord;
}
cmSelectAll:
{
  ClearMarker(lMarker);
  StartNewVisual(vtRotateVisual, vfTimer+vfBreak, ''#3'Выбор ...'#13#3, 1);
  _try
  {
    pushpos(#Groups);
    _loop Groups
    {
      InsertMarker(lMarker, Groups.Atl_Nrec);
      NextVisual;
    }
  } // try
  _except
   on ExUserBreak :
   {
     ClearMarker(lMarker);
   }
   _finally
   {
     poppos(#Groups);

     ReReadRecord(#Groups);
     StopVisual('',0);
   }
   SetWindowTitle(wnMainWindow, 'Выбор групп пользователей. Выбрано:'+ GetMarkerCount(lMarker));
   Rereadrecord(#Groups);
}

cmUnSelectAll:
{
  ClearMarker(lMarker);
  SetWindowTitle(wnMainWindow, 'Выбор групп пользователей. Выбрано:'+ GetMarkerCount(lMarker));
  Rereadrecord(#Groups);
}

cmInit:
{
 var CurRecord : comp;
 lmarker := initmarker(MarkerName, 8, 100, 10);
 GetMarker(lmarker, 0, CurRecord);

 if GetFirst Groups where ((CurRecord == Groups.Atl_Nrec)) = tsOk {}
 // message(Groups.Name);
 SetWindowTitle(wnMainWindow, 'Выбор групп пользователей. Выбрано:'+ GetMarkerCount(lMarker));
}
cmDone:
{
  donemarker(lmarker, MarkerName);
}
cmHotKeys :
  PutHotCommand(RunMenu('mnuSetGrpUserFilt'));
cmValue36:
{
  if ConditionActive(tcbySelected)
    PopCondition(tcbySelected)
  else
    PushCondition(tcbySelected);
ReReadRecord;
}

End;
End.
mnuSetGrpUserFilt Menu
{
-'Установить\снять фильтр по выбранным', cmValue36, '', , , , sci1Esc;
}
