//********************************************************************************
//
// Галактика 8.1 - Отчеты пользователя
// Отчеты на DSQL запросах
// Автор Стрельцов С.А. (отдел ПНР) 2012 год
//********************************************************************************
#include GetNotKAUFiltr.vih
#include SQLSetFilts.vih

Interface SQLSetFilts 'Настройка фильтров', EscClose;
  Show at (,,,20);

create view
 var cOborCFG : comp;
     wFltType,
     wOtherFlt : word;

 iGetNotKAUFiltr : GetNotKAUFiltr;

As select
  if (isValid(tnTabKau1),TabKau1.Name,
     if (LongInt(OborCFG.IDouble[1]) > cgiPickNotKAU, iGetNotKAUFiltr.GetFiltrName(LongInt(OborCFG.IDouble[1])), 'не выбран'))  (FieldName = NameTab1)
 ,if (isValid(tnTabKau2),TabKau2.Name,
     if (LongInt(OborCFG.IDouble[2]) > cgiPickNotKAU, iGetNotKAUFiltr.GetFiltrName(LongInt(OborCFG.IDouble[2])), 'не выбран'))  (FieldName = NameTab2)
 ,if (isValid(tnTabKau3),TabKau3.Name,
     if (LongInt(OborCFG.IDouble[3]) > cgiPickNotKAU, iGetNotKAUFiltr.GetFiltrName(LongInt(OborCFG.IDouble[3])), 'не выбран'))  (FieldName = NameTab3)
 ,if (isValid(tnTabKau4),TabKau4.Name,
     if (LongInt(OborCFG.IDouble[4]) > cgiPickNotKAU, iGetNotKAUFiltr.GetFiltrName(LongInt(OborCFG.IDouble[4])), 'не выбран'))  (FieldName = NameTab4)
 ,if (isValid(tnTabKau5),TabKau5.Name,
     if (LongInt(OborCFG.IDouble[5]) > cgiPickNotKAU, iGetNotKAUFiltr.GetFiltrName(LongInt(OborCFG.IDouble[5])), 'не выбран'))  (FieldName = NameTab5)
 ,if (isValid(tnTabKau6),TabKau6.Name,
     if (LongInt(OborCFG.IDouble[6]) > cgiPickNotKAU, iGetNotKAUFiltr.GetFiltrName(LongInt(OborCFG.IDouble[6])), 'не выбран'))  (FieldName = NameTab6)
 ,if (isValid(tnTabKau7),TabKau7.Name,
     if (LongInt(OborCFG.IDouble[7]) > cgiPickNotKAU, iGetNotKAUFiltr.GetFiltrName(LongInt(OborCFG.IDouble[7])), 'не выбран'))  (FieldName = NameTab7)
 ,
 *
From
  OborCFG
 ,KatKau
 ,KatKau     TabKau1
 ,KatKau     TabKau2
 ,KatKau     TabKau3
 ,KatKau     TabKau4
 ,KatKau     TabKau5
 ,KatKau     TabKau6
 ,KatKau     TabKau7
where
((
   cOborCFG == OborCFG.Nrec
   and OborCFG.WFILTER[1] == TabKau1.KodGroup
   and OborCFG.WFILTER[2] == TabKau2.KodGroup
   and OborCFG.WFILTER[3] == TabKau3.KodGroup
   and OborCFG.WFILTER[4] == TabKau4.KodGroup
   and OborCFG.WFILTER[5] == TabKau5.KodGroup
   and OborCFG.WFILTER[6] == TabKau6.KodGroup
   and OborCFG.WFILTER[7] == TabKau7.KodGroup
))
;

Procedure ReInit(_cOborCFG : comp);
{
  cOborCFG := _cOborCFG;
  if GetFirst OborCFG = tsOk {}
  ReReadRecord;
}

Window wCaseTypeFltr 'Выбор фильтра' doAccept, escClose, Dialog;
Show at (, ,40 ,7);
Screen scCaseTypeFltr 'Основная'(,,sci1Esc);
fields
  wFltType  ('Тип фильтра',,sci1Esc) : NoProtect;
buttons
  cmOk,Default,, 'Далее',,;
  cmCancel,,, 'Отмена',,;
<<

   (.) Каталог аналитик      `
   (.) Прочие `

    <.  Далее  .>     <. Отмена .>
>>
end;
handleEvent

cmOk: //Выбор фильтра
{
  CloseWindow(wCaseTypeFltr);
}

cmCancel:
{
}
end;
end; // Window

screen scSetFilts 'Настройка фильтров'('',,sci1Esc);
table OborCFG;
 fields
   OborCfg.WFILTER[8] ('Фильтр по дате'): NoProtect;
   OborCfg.sRep[8]  ('Название Фильтра по периоду'): NoProtect;
   OborCfg.sRep[0]  ('Название Фильтра по дате'): NoProtect;
   #declare NameTabField(Num)
    '#Num:' : {Font = {Bold = true}}, Skip, Centered;
    OborCfg.sRep[#Num] ('Название фильтра'): NoProtect;
    NameTab#Num ('Используемая для фильтрации внешняя таблица или группа КАУ',,sci1378Esc) :
      Protect, PickButton, {Font = {Color = ColorMark}};
  #end
  #NameTabField(1)
  #NameTabField(2)
  #NameTabField(3)
  #NameTabField(4)
  #NameTabField(5)
  #NameTabField(6)
  #NameTabField(7)
<<

   [.] Фильтр по периоду` .@@@@@@@@@@@@@@@@@@@@@@@
   [.] Дата `             .@@@@@@@@@@@@@@@@@@@@@@@
     Название
.@@.@@@@@@@@@@@@@@@@@@@@@@@@.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
.@@.@@@@@@@@@@@@@@@@@@@@@@@@.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
.@@.@@@@@@@@@@@@@@@@@@@@@@@@.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
.@@.@@@@@@@@@@@@@@@@@@@@@@@@.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
.@@.@@@@@@@@@@@@@@@@@@@@@@@@.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
.@@.@@@@@@@@@@@@@@@@@@@@@@@@.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
.@@.@@@@@@@@@@@@@@@@@@@@@@@@.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@


>>
end; // screen
HandleEvent

cmCheckField:
{
  case (CurField) of
    #OborCfg.WFILTER[8], #OborCfg.sRep[8], #OborCfg.sRep[0]:
    {
      update current OborCFG;
    }
    #declare CheckKatKau(Num)
      #OborCfg.sRep[#Num]:
      {
         if OborCfg.sRep[#Num] <> OldFieldValue
         {
           update current OborCFG;
         }
      }
    #end
    #CheckKatKau(1)
    #CheckKatKau(2)
    #CheckKatKau(3)
    #CheckKatKau(4)
    #CheckKatKau(5)
    #CheckKatKau(6)
    #CheckKatKau(7)
  end; // case
}

cmPick:
{
  case (CurField) of
    #declare PickKatKau(Num)
      #NameTab#Num:
      {
        RunWindowModal(wCaseTypeFltr)
        {
          if wFltType = 0
          {
            OborCFG.iDouble[#Num] := 0;
            if (RunInterface('F_AN::GetKauTable',cgiPick,cgKatKau_BuhDisabled,OborCFG.WFILTER[#Num],comp(0)) <> cmCancel)
            {
              if Trim(OborCfg.sRep[#Num]) = ''
              {
                OborCfg.sRep[#Num] := TabKau#Num.Name;
              }
                update current OborCFG;
            }
          }
          else
          {
             OborCFG.WFILTER[#Num] := 0;
             OborCFG.iDouble[#Num] := iGetNotKAUFiltr.GetCaseFiltrDialog;
             if Trim(OborCfg.sRep[#Num]) = ''
             {
               OborCfg.sRep[#Num] := iGetNotKAUFiltr.GetFiltrName(LongInt(OborCFG.iDouble[#Num]));
             }
             update current OborCFG;
             ReReadRecord;
          }
        }
      }
    #end
    #PickKatKau(1)
    #PickKatKau(2)
    #PickKatKau(3)
    #PickKatKau(4)
    #PickKatKau(5)
    #PickKatKau(6)
    #PickKatKau(7)
  end; // case
} // cmPick

cmDelOnProtect:
  {
    case (CurField) of
      #declare NamTabToZero(Num)
        #NameTab#Num:
        {
          set OborCFG.iDouble[#Num] := 0;
          set OborCFG.WFILTER[#Num] := 0;
          set OborCfg.sRep[#Num] = ''
          update current OborCFG;
        }
      #end
      #NamTabToZero(1)
      #NamTabToZero(2)
      #NamTabToZero(3)
      #NamTabToZero(4)
      #NamTabToZero(5)
      #NamTabToZero(6)
      #NamTabToZero(7)
    end; //case
  } //cmDelOnProtect

cmClose:
{
  update current OborCFG;
}
end;

end.
