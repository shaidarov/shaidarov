//********************************************************************************
//
// Галактика 8.1 - Отчеты пользователя
// Отчеты на DSQL запросах
// Автор Стрельцов С.А. (отдел ПНР) 2012 год
//********************************************************************************
#doc
  Интерфейс выбора нескольких пользователей
#end
Interface PickUsersNames 'Пользователи системы'   (,,sci1InsPM) DoAccept, EscClose, Cyan;
  show at ( 20, 3, 70, 18);

create view AllUsers
 var
   Markers      : LongInt;
   IndexNo      : LongInt;
   MarkerName   : string;
  (MarkerStr)

  As Select
   if (SearchMarker(Markers, X$Users.atl_nrec, IndexNo) = True, 'V', ' '),

   *
 from
   X$Users (USERBYLOGINNAME,ReadOnly)
 Condition bySelected = foundmarker(Markers, X$Users.atl_nrec)
 ;

parameters
  MarkerName
  ;
Panel pnTablesList;
Table X$Users;
  Browse bwTableList;
    Fields
        {Font = {Color = if (MarkerStr = 'V',ColorMark,0)}};
      MarkerStr        ''                     : [ 1],  Skip;
      X$Users.xu$UserOffice 'Офис' ('Номер офиса, в котором находится пользователь') : [3],Protect;
      X$Users.xu$loginname  'Сетевое имя'     : [15], Protect;
      X$Users.xu$FullName   'Полное имя'      : [25], Protect;
      sGetTuneEx('USER.DESGR', UserOfficeFilial(x$users.atl_nrec), x$users.Atl_NRec)
         'Группа' ('Группа дескрипторов, в которую входит пользователь') : [6], Protect;
    end;

  HandleEvent
      cmMarkUnmark:
        {
          InvertMarker(Markers, X$Users.atl_nrec);
          if (GetNext X$Users = tsOk) {};
          ReReadRecord(#X$Users);
          SetWindowTitle(wnMainWindow, 'Выбор пользователей. Выбрано:'+ GetMarkerCount(Markers));
        }
      cmSelectAll    :
        {
           PushPos(tnX$Users);
           if (GetFirst X$Users = tsOk)
             do
             {
               InsertMarker(Markers, X$Users.atl_nrec);
             }
             while (GetNext X$Users = tsOk)
           PopPos(tnX$Users);
           ReReadRecord(#X$Users);
           SetWindowTitle(wnMainWindow, 'Выбор пользователей. Выбрано:'+ GetMarkerCount(Markers));

         }

      cmUnselectAll:
      {
         ClearMarker(Markers);
         ReReadRecord(#X$Users);
         SetWindowTitle(wnMainWindow, 'Выбор пользователей. Выбрано:'+ GetMarkerCount(Markers));

      }
    end;
  end; // panel


HandleEvent
  cmInit :
   {
     var  datechanged : boolean;
     var  Rec         : comp;
     Markers:=InitMarker(MarkerName, 8, 50, 10);
     SetWindowTitle(wnMainWindow, 'Выбор пользователей. Выбрано:'+ GetMarkerCount(Markers));

   }

  cmDone  :
    DoneMarker(Markers, MarkerName);

 // cmCancel:
 //   ClearMarker(Markers);

  cmDefault :
   {
     if ( GetMarkerCount(Markers) = 0 )
       InsertMarker(Markers, X$Users.atl_nrec);
   }
cmHotKeys :
  PutHotCommand(RunMenu('mnuSetUserFilt'));
cmValue36:
{
  if ConditionActive(tcbySelected)
    PopCondition(tcbySelected)
  else
    PushCondition(tcbySelected);
ReReadRecord;
}

End;
End.
mnuSetUserFilt Menu
{
-'Установить\снять фильтр по выбранным', cmValue36, '', , , , sci1Esc;
}
