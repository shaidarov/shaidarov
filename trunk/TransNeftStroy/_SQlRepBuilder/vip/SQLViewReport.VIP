//********************************************************************************
//
// Галактика 8.1 - Отчеты пользователя
// Отчеты на DSQL запросах
// Автор Стрельцов С.А. (отдел ПНР) 2012 год
//********************************************************************************
//--------------------------------------------- основной интерфейс -------------------------------------------
#include ViewResult.vih
#include SQLSetFilts.vih
#include SQLFilts.vih
#include Query.vih

#include SQLBuildReport.vih
Interface SQLViewReport 'Отчеты на DSQL запросах (#(Vers))', EscClose, Cyan;
 Show at (,,,20);

Create view
var
 sDescr, sDesgr : string;
 ModulID : string;
 wSQLBuildReport : SQLBuildReport;
As Select
 *
from
  OborCfg,
  AttrNam,
  AttrVal,
  OborPick,
  tempOborCFG,
  OborPick OborPickMod,
  OborPick OborPickUser,
  OborPick OborPickUserGrp,
  OborPick OborPickAdmin,
  OborPick OborPickAdminGrp

 where
((
    word(1000) == OborCfg.vRep
    and    LNT_ItemName == CONTOURCOMPONENTS.Name(NoIndex)
    and UserID          == X$users.Atl_Nrec

 //   and
 //    (  LNT_ItemName = 'NASTR_G'


    and OborCfg.Nrec   == OborPickUser.cOborCfg
    and              4 == OborPickUser.wList

    and OborCfg.Nrec   == OborPickUserGrp.cOborCfg
    and              5 == OborPickUserGrp.wList

    and OborCfg.Nrec   == OborPickAdmin.cOborCfg
    and              6 == OborPickAdmin.wList

    and OborCfg.Nrec   == OborPickAdminGrp.cOborCfg
    and              7 == OborPickAdminGrp.wList
))
;

//------------------------------------------------------------------------------------------------------------
browse ViewReports ('Отчеты на SQL запросах' ,, sci1478EnEsc);
 table tempOborCfg;
 fields
  tempOborCfg.nRep    #3'Номер'         : [3], Protect;
  tempOborcfg.REPNAME #3'Наименование'  : [50], Protect;
  tempOborCFG.Descr   #3'Создал'        : [5], Protect;
  tempOborCFG.dRep    #3'Дата'          : [6], Protect;
end; // brKrossMC
//------------------------------------------------------------------------------------------------------------
function HasAccessToView : boolean;
{
  HasAccessToView := false;
  // проверка доступа
  // права не розданы - доступен всем
  if GetFirst OborPickUser <> tsOk and GetFirst OborPickUserGrp <> tsOk
      HasAccessToView := true
  else
    if sGetTune('User.Descr') = OborCFG.Descr // по дескриптору создателя
      HasAccessToView := true
    else // проверяем права получения
      {       // выбран пользователь в правах получения
        if GetFirst OborPickUser where ((OborCfg.Nrec  == OborPickUser.cOborCfg
                                     and             4 == OborPickUser.wList
                                     and UserId        == OborPickUser.cRec (NoIndex)))  = tsOk
          HasAccessToView := true;
        else  //выбрана группа пользователя в правах получения
          _loop OborPickUserGrp
          {
            if GetFirst GROUPUSERS where ((OborPickUserGrp.cRec == GROUPUSERS.GROUPCODE
                                           and UserID           == GROUPUSERS.USERCODE )) = tsOk
           {
             HasAccessToView := true;
             break;
           }
          }
      }
}
function HasAccessToEdit : boolean;
{
  HasAccessToEdit := false;
  if sGetTune('User.Descr') = OborCFG.Descr
    HasAccessToEdit := true;
  else// проверка прав редактирования
  if GetFirst OborPickAdmin = tsOk or GetFirst OborPickAdminGrp = tsOk
  {       // выбран пользователь в правах редактирования
    if GetFirst OborPickAdmin where ((OborCfg.Nrec  == OborPickAdmin.cOborCfg
                                  and             6 == OborPickAdmin.wList
                                  and UserId        == OborPickAdmin.cRec(NoIndex) ))  = tsOk
      HasAccessToEdit := true;
    else  //выбрана группа пользователя в правах редактирования
      _loop OborPickAdminGrp
      {
        if GetFirst GROUPUSERS where ((OborPickAdminGrp.cRec == GROUPUSERS.GROUPCODE
                                        and UserID           == GROUPUSERS.USERCODE )) = tsOk
        {
          HasAccessToEdit := true;
          break;
        }
      }
  }
  else
    HasAccessToEdit := true
}
procedure InsRep;
{
  if (NOT NextVisual)
  {
    if (VisualizationActive())
      stopvisual('',0);
    Exit;
  }
  ClearBuffer(#tempOborCFG);
  tempOborCFG.Nrec    := OborCFG.Nrec;
  tempOborCFG.nRep    := OborCFG.nRep;
  tempOborCFG.REPNAME := OborCFG.REPNAME;
  tempOborCFG.Descr   := OborCFG.Descr;
  tempOborCFG.dRep    := OborCFG.dRep;
  insert current tempOborCFG;
}
procedure GetReportList;
{
  StartNewVisual(vtNumericVisual,vfTimer, 'Формирование списка отчетов', 1);
  Delete all from tempOborCFG;
  _Loop OborCFG
  {
    case wGetTune('USERTUNE.AccessToViewSQlRep') of
    0:
    { if GetFirst OborPickMod = tsOk
        if GetFirst CONTOURCOMPONENTS = tsOk
          if GetFirst OborPickMod where (( OborCfg.Nrec   == OborPickMod.cOborCfg
                                         and           3  == OborPickMod.wList
                                         and CONTOURCOMPONENTS.Atl_Nrec == OborPickMod.cRec(NoIndex)))=tsOk
             if HasAccessToView or HasAccessToEdit
               InsRep;
    }
    1:
    { if GetFirst CONTOURCOMPONENTS = tsOk
        if GetFirst OborPickMod where (( OborCfg.Nrec   == OborPickMod.cOborCfg
                                       and           3  == OborPickMod.wList
                         and CONTOURCOMPONENTS.Atl_Nrec == OborPickMod.cRec(NoIndex)))=tsOk

        InsRep;
    }
    2:if HasAccessToView or HasAccessToEdit
              InsRep;

    3:InsRep;
    end
  }
  if (VisualizationActive())
    stopvisual('',0);

  If GetFirst TempOborCFG = tsOk {}
}

HandleEvent
cmInit:
{
  var PathToXLT : string;
 // PopBounds(tbbyMod);
 // if LNT_ItemName <> 'NASTR_G'
 //   Pushbounds(tbbyMod);
 // ReReadRecord;
  //message(LNT_ItemName);
  GetReportList;

  if boGetTune('USERTUNE.AccessToCreateSQlRep')=false
    SetHelpAndStatusContext (ViewReports,-1, sci1EnEsc, '');
 #ifdef Atl51
   // if not ExistDir(TranslatePath('%ClientStartPath%') + 'XLS\SQLReports')
    set PathToXLT := TranslatePath('%ClientStartPath%') + 'XLS\DSQlBuildReps\ReadMe.txt';
  #else
    Set PathToXLT := StartPathName + 'XLS\DSQlBuildReps\ReadMe.txt';
  #endif
  if Not ExistFile(PathToXLT)
    LogStrToFile(PathToXLT,'Папка предназначена для хранения автоматически созданных шаблонов в интерфейсе "Отчеты на SQL запросах"')
}
cmDefault:
{
 if Not IsValidAll(#tempOborCFG)
    exit;
  var sDescr, sDesgr : string;
  var HasAccess : boolean;
  HasAccess := false;
  // проверка доступа
  // права не розданы - доступен всем
  if GetFirst OborCFG where ((tempOborCFG.Nrec == OborCFG.Nrec))=tsOk
    HasAccess := HasAccessToView;
    if not HasAccess   // проверка прав редактирования
      HasAccess := HasAccessToEdit;

  if not HasAccess
    Message('Вы не имеете прав на получение данного отчета!',Error);
  else
    RunInterFace(SQLRunReport,tempOborCFG.Nrec);
}
cmInsert:
{
  if boGetTune('USERTUNE.AccessToCreateSQlRep')=true
  {
    wSQLBuildReport.SQLInsert;
    GetReportList;
  }
  else
  {
    Message('Вы не имеете прав на создание отчетов!',Error);
    Stop;
    Abort;
    Exit;
  }
 // message('Insert');
}
cmSetDefault:
{

}

cmEdit:
{
 if Not IsValidAll(#tempOborCFG)
    exit;
  wSQLBuildReport.SQLEdit(tempOborCFG.Nrec);
}
cmDelete:
{
  if Not IsValidAll(#tempOborCFG)
    exit;

  if GetFirst OborCFG where ((tempOborCFG.Nrec == OborCFG.Nrec))=tsOk
    if not HasAccessToEdit
    {
        Message('Вы не имеете прав на удаление данного отчета!',Error);
        Stop;
        Abort;
        Exit;
    }

  if (Message('Удалить текущую отчет?', yesNo+Confirmation) <> cmYes)
    Stop
  else
  {
    if GetFirst UserDeskRep where ((tempOborCFG.Nrep + cgReport_30000  == UserDeskRep.nRep)) = tsOk
    {
      Delete PickRep where (( UserDeskRep.NRec == PickRep.cUserDeskRep))

    }
    Delete UserDeskRep where ((tempOborCFG.Nrep + cgReport_30000  == UserDeskRep.nRep));

    if GetFirst AttrNam where (( 'SQl-запрос' == AttrNam.Name
                               and word(9400) == AttrNam.WTABLE)) = tsOk
      if GetFirst AttrVal where (( word(9400)   == AttrVal.WTABLE
                               and tempOborCfg.Nrec == AttrVal.cRec
                               and AttrNam.Nrec == AttrVal.CATTRNAM)) =tsOk
        delete current AttrVal;
    if GetFirst AttrNam where (( 'Макрос' == AttrNam.Name
                               and word(9400) == AttrNam.WTABLE)) = tsOk
      if GetFirst AttrVal where (( word(9400)   == AttrVal.WTABLE
                               and tempOborCfg.Nrec == AttrVal.cRec
                               and AttrNam.Nrec == AttrVal.CATTRNAM)) =tsOk
        delete current AttrVal;
    Delete OborPick where ((tempOborCfg.Nrec  == OborPick.cOborCfg));
    Delete OborCFG where ((tempOborCFG.Nrec == OborCFG.Nrec));
    GetReportList;
  }
}
end;
end.
